buildscript {
	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "gradle.plugin.com.github.gradle-git-version-calculator:gradle-git-version-calculator:1.0.0"
	}
}

plugins {
	id 'nu.studer.credentials' version '1.0.4'
}

ext {
	publish_user = project.hasProperty('publish_user') ? project.getProperty('publish_user') : credentials.publish_user
	publish_pass = project.hasProperty('publish_pass') ? project.getProperty('publish_pass') : credentials.publish_pass
}

def generateBaseName(artifact_id) {
	return "paseto-" + artifact_id
}

allprojects {
	apply plugin: 'java-library'
	apply plugin: 'com.github.gradle-git-version-calculator'
	apply plugin: 'jacoco'

	gitVersionCalculator {
		prefix = 'release@'
		withSnapshot = true
	}

	ext {
		group_id = "net.aholbrook.paseto"
		library_version = gitVersionCalculator.calculateVersion()
	}

	repositories {
		mavenLocal()
		mavenCentral()
		maven {
			url  "https://dl.bintray.com/terl/lazysodium-maven"
		}
	}
}

subprojects {
	dependencies {
		testImplementation 'junit:junit:4.12'
	}

	jacocoTestReport {
		additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
		sourceDirectories = files(sourceSets.main.allSource.srcDirs)
		classDirectories =  files(sourceSets.main.output)
		reports {
			html.enabled = true
			xml.enabled = true
			csv.enabled = false
		}
	}
}

task allDependencies {
	subprojects.each { dependsOn("${it.name}:dependencies") }
}

task jacocoRootReport(type: JacocoReport) {
	dependsOn = subprojects.test
	additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
	sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
	classDirectories = files(subprojects.sourceSets.main.output)
	executionData = files(subprojects.jacocoTestReport.executionData)
	reports {
		html.enabled = true
		xml.enabled = true
		csv.enabled = false
	}
	onlyIf = {
		true
	}
	doFirst {
		executionData = files(executionData.findAll {
			it.exists()
		})
	}

	afterEvaluate {
		classDirectories = files(classDirectories.files.collect {
			fileTree(dir: it, exclude: 'net/aholbrook/paseto/test/**')
		})
	}
}

check.dependsOn jacocoRootReport
